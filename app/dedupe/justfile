# Dedupe project automation

default:
    @just --list

gen:
    uv run gen-pydantic def/models.yaml > gen/models.py
    uv run gen-pydantic def/commands.yaml > gen/commands.py
    uv run gen-pydantic def/queries.yaml > gen/queries.py
    uv run gen-pydantic def/events.yaml > gen/events.py
    uv run gen-pydantic def/mutations.yaml > gen/mutations.py

    uv run python -m dizzy.utils.generate_query_interfaces def/queries.yaml queries/interfaces.py
    uv run python -m dizzy.utils.generate_mutation_interfaces def/mutations.yaml mutations/interfaces.py
    uv run python -m dizzy.utils.generate_procedure_contexts def/procedures.d.yaml gen/procedures.py procedures/interfaces.py


check:
    uv run ty check queries/

test:
    uv run pytest tests/

# Deployment recipes
deploy-install:
    cd deploy && pulumi install

deploy-config-secrets user="admin" password="changeme":
    cd deploy && pulumi config set --secret minio:minio_user "{{user}}"
    cd deploy && pulumi config set --secret minio:minio_password "{{password}}"

deploy-up:
    cd deploy && pulumi up

deploy-preview:
    cd deploy && pulumi preview

deploy-destroy:
    cd deploy && pulumi destroy

deploy-status:
    cd deploy && pulumi stack output
    @echo ""
    kubectl get all -n minio

deploy-verify:
    @echo "Checking Pulumi outputs..."
    cd deploy && pulumi stack output
    @echo ""
    @echo "Checking Kubernetes resources..."
    kubectl get all -n minio
    @echo ""
    @echo "Testing API connectivity..."
    curl -s http://localhost:30000/minio/health/live || echo "API not responding"
    @echo ""
    @echo "Minio console available at: http://localhost:30001"

deploy-full: deploy-install deploy-up deploy-verify

deploy-logs:
    kubectl logs -n minio -l app=minio --tail=50 -f

